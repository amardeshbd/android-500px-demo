apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    defaultConfig {
        applicationId "com.hossainkhan.android.dpx"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        buildConfigField "String", "API_KEY", "\"" + getLocalProperty("API_KEY") + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'


        }
    }

    dataBinding {
        // Use DataBinding for MVVM
        // https://developer.android.com/topic/libraries/data-binding/start.html
        enabled = true
    }

    androidExtensions {
        experimental = true
    }
}

def getLocalProperty(propertyKey) {
    // Creates a variable called localPropertiesFile, and initializes it to the local.properties file.
    def localPropertiesFile = rootProject.file("local.properties")
    if (!localPropertiesFile.exists()) {
        return "CI-SERVER - NO KEY"
    }

    // Loads the local.properties file into the properties object.
    def properties = new Properties()
    properties.load(new FileInputStream(localPropertiesFile))

    return properties[propertyKey]
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX Libs
    implementation "androidx.appcompat:appcompat:$rootProject.androidxVersion"
    implementation "androidx.core:core-ktx:$rootProject.androidxCoreVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$rootProject.androidxRecyclerViewVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.androidxLifecycleVersion"
    implementation "android.arch.lifecycle:reactivestreams:1.1.1"
    implementation 'com.google.android.material:material:1.0.0'

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"

    // Gson for JSON
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // Timber for Logging
    implementation "com.jakewharton.timber:timber:$rootProject.timberLibraryVersion"

    // Picasso
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"


    // Testing related libs
    // --------------------------------
    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test:runner:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"
}
